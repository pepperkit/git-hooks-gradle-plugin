plugins {
    id 'java-library'
    id 'java-gradle-plugin'
    id 'jvm-test-suite'

    id 'checkstyle'
    id 'org.sonarqube' version '4.4.1.3373'
    id 'jacoco'

    id 'maven-publish'

//    id 'io.github.pepperkit.git-hooks-gradle-plugin' version '1.0.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

group 'io.github.pepperkit'
version '1.0.0'

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

gradlePlugin {
    plugins {
        gitHooksGradlePlugin {
            id = 'io.github.pepperkit.git-hooks-gradle-plugin'
            implementationClass = 'io.github.pepperkit.githooks.GitHooksPlugin'
        }
    }
}

checkstyle {
    toolVersion '10.12.2'
    config project.resources.text
            .fromUri(new URI("https://raw.githubusercontent.com/pepperkit/java-style-guide/v1.0.2-9.2/checkstyle.xml"))
}

testing {
    suites {
        test {
            useJUnitJupiter()

            dependencies {
                implementation 'org.junit.jupiter:junit-jupiter:5.10.0'
                implementation 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
                implementation 'org.junit.platform:junit-platform-suite:1.10.0'
                implementation 'org.mockito:mockito-core:5.4.0'
                implementation 'org.assertj:assertj-core:3.24.2'

            }
        }

        systemTest(JvmTestSuite) {
            useJUnitJupiter()

            sources {
                java {
                    srcDirs = ['src/system-test/java']
                }
                resources {
                    srcDirs = ['src/system-test/resources']
                }
            }

            dependencies {
                implementation project()

                implementation 'org.junit.jupiter:junit-jupiter:5.10.0'
                implementation 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
                implementation 'org.junit.platform:junit-platform-suite:1.10.0'
                implementation 'org.mockito:mockito-core:5.4.0'
                implementation 'org.assertj:assertj-core:3.24.2'

                implementation('org.testcontainers:junit-jupiter:1.18.3')

                implementation("io.cucumber:cucumber-java:7.13.0")
                implementation("io.cucumber:cucumber-junit-platform-engine:7.13.0")
            }
        }
    }
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
        html.required = true
        xml.destination file("${buildDir}/reports/jacoco.xml")
    }
}

sonar {
    properties {
        property "sonar.projectKey", "pepperkit_git-hooks-gradle-plugin"
        property "sonar.organization", "pepperkit"
        property "sonar.host.url", "https://sonarcloud.io"
        // property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco.xml"
    }
}

/*

apply plugin: 'io.github.pepperkit.git-hooks-gradle-plugin'

gitHooksGradlePlugin {
    hooks = [
        'pre-commit' : 'gradle -q checkstyleMain',
        'pre-push' : 'gradle -q test'
    ]
}
*/
